AC_INIT(configure.in)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(ncmpcpp, 0.1.1)

AC_PREREQ(2.59)

AC_LANG_CPLUSPLUS
AC_PROG_CXX
AM_PROG_LIBTOOL

AC_ARG_ENABLE(unicode, AS_HELP_STRING([--enable-unicode], [Enable utf8 support]), [unicode=$enableval], [unicode=yes])
AC_ARG_WITH(taglib, AS_HELP_STRING([--with-taglib], [Enable tag editor]), [taglib=$withval], [taglib=no])

dnl ========================
dnl = checking for ncurses =
dnl ========================
if test "$unicode" = "yes" ; then
	AC_PATH_PROG(NCURSESW5_CONFIG, ncursesw5-config)
	if test "$NCURSESW5_CONFIG" != "" ; then
		CPPFLAGS="$CPPFLAGS -DUTF8_ENABLED `$NCURSESW5_CONFIG --cflags`"
		LDFLAGS="$LDFLAGS `$NCURSESW5_CONFIG --libs`"
		AC_CHECK_LIB(ncursesw, initscr, , AC_MSG_ERROR([ncursesw library is required]))
	else
		AC_MSG_ERROR([ncursesw5-config executable is missing])
	fi
else
	AC_PATH_PROG(NCURSES5_CONFIG, ncurses5-config)
	if test "$NCURSES5_CONFIG" != "" ; then
		CPPFLAGS="$CPPFLAGS `$NCURSES5_CONFIG --cflags`"
		LDFLAGS="$LDFLAGS `$NCURSES5_CONFIG --libs`"
		AC_CHECK_LIB(ncurses, initscr, , AC_MSG_ERROR([ncurses library is required]))
	else
		AC_MSG_ERROR([ncurses5-config executable is missing])
	fi
fi
AC_CHECK_HEADERS([ncurses.h], , AC_MSG_ERROR([missing ncurses.h header]))


dnl =======================
dnl = checking for libmpd =
dnl =======================
PKG_CHECK_MODULES([libmpd], libmpd >= 0.15.3, , AC_MSG_ERROR([libmpd-0.15.3 or higher is required]))
AC_SUBST(libmpd_CFLAGS)
AC_SUBST(libmpd_LIBS)
CPPFLAGS="$CPPFLAGS $libmpd_CFLAGS"
LDFLAGS="$LDFLAGS $libmpd_LIBS"
AC_CHECK_HEADERS([libmpd/libmpd.h], , AC_MSG_ERROR([missing libmpd.h header]))

dnl ========================
dnl = checking for pthread =
dnl ========================
AC_CHECK_LIB(pthread, pthread_create, LDFLAGS="$LDFLAGS -pthread", AC_MSG_ERROR([missing pthread library]))
AC_CHECK_HEADERS([pthread.h], , AC_MSG_ERROR([missing pthread.h header]))

dnl =======================
dnl = checking for taglib =
dnl =======================
if test "$taglib" = "yes" ; then
	AC_PATH_PROG(TAGLIB_CONFIG, taglib-config)
	if test "$TAGLIB_CONFIG" != "" ; then
		CPPFLAGS="$CPPFLAGS `$TAGLIB_CONFIG --cflags`"
		PKG_CHECK_MODULES([taglib], taglib, LDFLAGS="$LDFLAGS `$TAGLIB_CONFIG --libs`", AC_MSG_ERROR([taglib library is required]))
		AC_CHECK_HEADERS([taglib.h], , AC_MSG_ERROR([missing taglib.h header]))
	else
		AC_MSG_ERROR([taglib-config executable is missing])
	fi
fi


AC_OUTPUT(Makefile src/Makefile)
